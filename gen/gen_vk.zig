// Copyright (c) 2026 Egor Lazarchuk
// SPDX-License-Identifier: MIT

const std = @import("std");
const Allocator = std.mem.Allocator;
const XmlParser = @import("xml_parser.zig");
const XmlDatabase = @import("vk_database.zig").XmlDatabase;
const TypeDatabase = @import("vk_database.zig").TypeDatabase;

const IN_PATH = "gen/vk.xml";
const OUT_PATH = "src/vk.zig";
const HEADER =
    \\// This file is auto generated by gen/{s}
    \\
    \\
;

pub fn main() !void {
    var arena: std.heap.ArenaAllocator = .init(std.heap.page_allocator);
    const alloc = arena.allocator();

    const xml_file = try std.fs.cwd().openFile(IN_PATH, .{});
    const buffer = try alloc.alloc(u8, (try xml_file.stat()).size);
    _ = try xml_file.readAll(buffer);

    const xml_db: XmlDatabase = try .init(alloc, buffer);
    var type_db: TypeDatabase = try .from_xml_database(alloc, &xml_db);

    std.fs.cwd().deleteFile(OUT_PATH) catch {};
    const file = try std.fs.cwd().createFile(OUT_PATH, .{});
    defer file.close();
    var writer: Writer = try .init(alloc, file);
    defer writer.flush();

    var tmp_arena: std.heap.ArenaAllocator = .init(std.heap.page_allocator);
    const tmp_alloc = tmp_arena.allocator();

    writer.write(HEADER, .{@src().file});
    write_constants(&writer, &xml_db);
    write_versions(&writer);
    write_handles(&writer, &type_db);
    try write_bitfields(&writer, &type_db);
    try write_enums(&writer, &type_db);
    try write_structs(tmp_alloc, &writer, &type_db);
    _ = tmp_arena.reset(.retain_capacity);
    try write_unions(tmp_alloc, &writer, &type_db);
    _ = tmp_arena.reset(.retain_capacity);
    try write_functions(tmp_alloc, &writer, &type_db);
    _ = tmp_arena.reset(.retain_capacity);
    try write_aliases(tmp_alloc, &writer, &type_db);
    _ = tmp_arena.reset(.retain_capacity);
    write_unknown_types(&writer, &type_db);
    write_extensions(&writer, &xml_db);
}

const Writer = struct {
    writer: std.fs.File.Writer,
    alloc: Allocator,

    const Self = @This();
    pub fn init(alloc: Allocator, file: std.fs.File) !Self {
        const buffer = try alloc.alloc(u8, 4096 * 12);
        const writer = file.writer(buffer);
        const result: Self = .{ .writer = writer, .alloc = alloc };
        return result;
    }

    fn flush(self: *Self) void {
        _ = self.writer.interface.flush() catch unreachable;
    }

    fn write(self: *Self, comptime fmt: []const u8, args: anytype) void {
        const line = std.fmt.allocPrint(self.alloc, fmt, args) catch unreachable;
        defer self.alloc.free(line);
        _ = self.writer.interface.write(line) catch unreachable;
    }

    fn write_comment(self: *Self, comment: []const u8, line_start: []const u8) void {
        var iter = std.mem.splitScalar(u8, comment, '\n');
        while (iter.next()) |line| {
            const trimmed_line = std.mem.trimStart(u8, line, " ");
            if (trimmed_line.len == 0) break;

            _ = self.writer.interface.write(line_start) catch unreachable;
            _ = self.writer.interface.write(trimmed_line) catch unreachable;
            _ = self.writer.interface.write("\n") catch unreachable;
        }
    }
};

fn write_constants(w: *Writer, xml_db: *const XmlDatabase) void {
    w.write(
        \\// Constants
        \\
    , .{});
    for (xml_db.constants.items) |*c| {
        switch (c.value) {
            .invalid => {},
            .u32 => |v| w.write("pub const {s}: u32 = {d};\n", .{ c.name, v }),
            .u64 => |v| w.write("pub const {s}: u64 = {d};\n", .{ c.name, v }),
            .f32 => |v| w.write("pub const {s}: f32 = {d};\n", .{ c.name, v }),
        }
    }
}

fn write_versions(w: *Writer) void {
    w.write(
        \\// Versions
        \\pub const ApiVersion = packed struct(u32) {{
        \\    patch: u12 = 0,
        \\    minor: u10 = 0,
        \\    major: u7 = 0,
        \\    variant: u3 = 0,
        \\
        \\    pub fn less(v1: ApiVersion, v2: ApiVersion) bool {{
        \\        const v1_u32: u32 = @bitCast(v1);
        \\        const v2_u32: u32 = @bitCast(v2);
        \\        const result = v1_u32 < v2_u32;
        \\        return result;
        \\    }}
        \\}};
        \\pub const VK_API_VERSION_1_0: ApiVersion = .{{
        \\    .variant = 0,
        \\    .major = 1,
        \\    .minor = 0,
        \\    .patch = 0,
        \\}};
        \\pub const VK_API_VERSION_1_1: ApiVersion = .{{
        \\    .variant = 0,
        \\    .major = 1,
        \\    .minor = 1,
        \\    .patch = 0,
        \\}};
        \\pub const VK_API_VERSION_1_2: ApiVersion = .{{
        \\    .variant = 0,
        \\    .major = 1,
        \\    .minor = 2,
        \\    .patch = 0,
        \\}};
        \\pub const VK_API_VERSION_1_3: ApiVersion = .{{
        \\    .variant = 0,
        \\    .major = 1,
        \\    .minor = 3,
        \\    .patch = 0,
        \\}};
        \\pub const VK_API_VERSION_1_4: ApiVersion = .{{
        \\    .variant = 0,
        \\    .major = 1,
        \\    .minor = 4,
        \\    .patch = 0,
        \\}};
        \\
    , .{});
}

fn write_handles(w: *Writer, type_db: *const TypeDatabase) void {
    w.write(
        \\
        \\// Handles
        \\
    , .{});
    for (type_db.handles.items) |handle| {
        if (handle.objtypeenum) |s| w.write(
            \\// Type enum: {s}
            \\
        , .{s});

        if (handle.parent) |s| w.write(
            \\// Parent: {s}
            \\
        , .{s});
        w.write(
            \\pub const {s} = enum(u64) {{ none = 0, _ }};
            \\
        , .{handle.name});
    }
}

fn write_bitfields(w: *Writer, type_db: *const TypeDatabase) !void {
    w.write(
        \\
        \\// Empty bitmasks
        \\
    , .{});

    for (type_db.bitfields.items) |bitfield| {
        w.write(
            \\pub const {s} = packed struct(u{d}) {{
            \\
        , .{ bitfield.name, bitfield.backing_integer_width });

        var last_bitpos: ?u32 = null;
        for (bitfield.bits, 0..) |bit, i| {
            if (last_bitpos) |lb| {
                const diff = bit.bit - lb;
                if (1 < diff) w.write(
                    \\    _{d}: u{d} = 0,
                    \\
                , .{ lb, diff - 1 });
            } else {
                if (bit.bit != 0) w.write(
                    \\    _0: u{d} = 0,
                    \\
                , .{bit.bit});
            }
            last_bitpos = bit.bit;

            if (bit.enabled_by_extension) |e| w.write(
                \\    // Extension: {s}
                \\
            , .{e});
            // Some extensions add same bits, so check consecutive bits
            if (i < bitfield.bits.len - 1 and bit.bit == bitfield.bits[i + 1].bit) {
                //
            } else {
                if (bit.comment) |c| w.write(
                    \\    // Comment: {s}
                    \\
                , .{c});

                // TODO: lowercase names without prefix
                w.write(
                    \\    // bit: {d}
                    \\    {s}: bool = false,
                    \\
                , .{ bit.bit, bit.name });
            }
        }
        if (last_bitpos) |lb| {
            const last_element_width = bitfield.backing_integer_width - lb - 1;
            if (last_element_width != 0) w.write(
                \\    _: u{d} = 0,
                \\
            , .{last_element_width});
        } else {
            w.write(
                \\    _: u{d} = 0,
                \\
            , .{bitfield.backing_integer_width});
        }

        // all constants
        for (bitfield.constants) |constant| {
            if (constant.comment) |c| w.write(
                \\    // {s}
                \\
            , .{c});
            // TODO: lowercase names without prefix
            w.write(
                \\    pub const {s}: @This() = @bitCast(@as(u{d}, 0x{x}));
                \\
            , .{ constant.name, bitfield.backing_integer_width, constant.value });
        }
        // NOTE: Bitmasks do not have expandable constants
        w.write(
            \\}};
            \\
        , .{});
    }
}

fn write_enums(w: *Writer, type_db: *const TypeDatabase) !void {
    w.write(
        \\
        \\// Enums
        \\
    , .{});
    for (type_db.enums.items) |@"enum"| {
        w.write(
            \\pub const {s} = enum(i{d}) {{
            \\
        , .{ @"enum".name, @"enum".backing_integer_width });
        for (@"enum".values, 0..) |value, i| {
            if (value.enabled_by_extension) |e| w.write(
                \\    // Extension: {s}
                \\
            , .{e});

            // Some extensions add same enum values, so check consecutive bits
            if (i < @"enum".values.len - 1 and value.value == @"enum".values[i + 1].value) {
                //
            } else {
                if (value.comment) |c| w.write(
                    \\    // Comment: {s}
                    \\
                , .{c});

                w.write(
                    \\    {s} = {d},
                    \\
                , .{ value.name, value.value });
            }
        }

        w.write(
            \\    _,
            \\}};
            \\
        , .{});
    }
}

fn write_structs(alloc: Allocator, w: *Writer, type_db: *TypeDatabase) !void {
    w.write(
        \\
        \\// Structs
        \\
    , .{});

    for (type_db.structs.items) |@"struct"| {
        for (@"struct".enabled_by_extensions) |ext| w.write(
            \\// Extension: {s}
            \\
        , .{ext});

        if (@"struct".comment) |c| w.write(
            \\// Comment: {s}
            \\
        , .{c});

        if (@"struct".extends.len != 0) {
            w.write(
                \\// Extends: 
            , .{});
            for (@"struct".extends, 0..) |e, i| {
                const t = type_db.get_type(e);
                const s = type_db.get_struct(t.struct_idx());
                w.write(
                    \\{s}
                , .{s.name});
                if (i != @"struct".extends.len - 1) {
                    w.write(
                        \\,
                    , .{});
                }
            }
            w.write(
                \\
                \\
            , .{});
        }
        w.write(
            \\// Returned only: {}
            \\// Allow duplicate in pNext chain: {}
            \\pub const {s} = extern struct {{
            \\
        , .{ @"struct".returnedonly, @"struct".allowduplicate, @"struct".name });

        var packed_field_idx: u8 = 0;
        for (@"struct".fields) |f| {
            switch (f) {
                .single_field => |field| {
                    if (field.len_expression) |len| w.write(
                        \\    // Length expression: {s}
                        \\
                    , .{len});
                    if (field.stride) |stride| w.write(
                        \\    // Stride field: {s}
                        \\
                    , .{stride});
                    if (field.deprecated) |deprecated| w.write(
                        \\    // Deprecated: {s}
                        \\
                    , .{deprecated});
                    w.write(
                        \\    // Extern sync: {}
                        \\    // Optional: {}
                        \\
                    , .{ field.externsync, field.optional });
                    if (field.selector) |selector| w.write(
                        \\    // Selector field: {s} (What union field is valid)
                        \\
                    , .{selector});
                    if (field.objecttype) |objecttype| w.write(
                        \\    // Object type: {s} (Which object handle is this)
                        \\
                    , .{objecttype});
                    if (field.featurelink) |featurelink| w.write(
                        \\    // Feature link: {s}
                        \\
                    , .{featurelink});
                    if (field.comment) |comment| w.write(
                        \\    // Comment: {s}
                        \\
                    , .{comment});

                    try write_single_struct_field(
                        alloc,
                        type_db,
                        w,
                        @"struct".name,
                        &field,
                        field.type_idx,
                    );
                },
                .packed_field => |field| {
                    w.write(
                        \\    packed_field{d}: packed struct(u{d}) {{
                        \\
                    , .{ packed_field_idx, field.backing_integer_width });
                    for (field.parts) |part| {
                        w.write(
                            \\        {s}: u{d} = 0,
                            \\
                        , .{ part.name, part.bits });
                    }
                    w.write(
                        \\    }} = .{{}},
                        \\
                    , .{});
                    packed_field_idx += 1;
                },
            }
        }
        w.write(
            \\}};
            \\
        , .{});
    }
}

fn write_single_struct_field(
    alloc: Allocator,
    type_db: *const TypeDatabase,
    w: *Writer,
    struct_name: []const u8,
    field: *const TypeDatabase.Struct.SingleField,
    type_idx: TypeDatabase.Type.Idx,
) !void {
    const t = type_db.get_type(type_idx);
    const type_str = try type_db.type_string(alloc, type_idx);
    switch (t.*) {
        .base => |base| {
            switch (base) {
                .builtin => |_| {
                    if (std.mem.endsWith(u8, field.name, "Version")) {
                        w.write(
                            \\    {s}: ApiVersion = .{{}},
                            \\
                        , .{field.name});
                    } else {
                        w.write(
                            \\    {s}: {s} = 0,
                            \\
                        , .{ field.name, type_str });
                    }
                },
                .handle_idx => |_| {
                    w.write(
                        \\    {s}: {s} = .none,
                        \\
                    , .{ field.name, type_str });
                },
                .struct_idx => |struct_idx| {
                    var should_have_default: bool = true;
                    const s = type_db.get_struct(struct_idx);
                    for (s.fields) |f| {
                        if (f == .single_field) {
                            const tt = type_db.get_type(f.single_field.type_idx);
                            if (tt.union_idx() != .none)
                                should_have_default = false;
                        }
                    }
                    var default_str: []const u8 = &.{};
                    if (should_have_default)
                        default_str = " = .{}";

                    w.write(
                        \\    {s}: {s}{s},
                        \\
                    , .{ field.name, type_str, default_str });
                },
                .bitfield_idx => |_| {
                    w.write(
                        \\    {s}: {s} = .{{}},
                        \\
                    , .{ field.name, type_str });
                },
                .enum_idx => |enum_idx| {
                    // Should only be valid for sType: VkStructureType
                    if (field.stype_value) |v| {
                        w.write(
                            \\    pub const STYPE = VkStructureType.{s};
                            \\    {s}: {s} = @This().STYPE,
                            \\
                        , .{ v, field.name, type_str });
                    } else {
                        const e = type_db.get_enum(enum_idx);
                        w.write(
                            \\    {s}: {s} = .{s},
                            \\
                        , .{ field.name, type_str, e.values[0].name });
                    }
                },
                .union_idx => |_| {
                    w.write(
                        \\    {s}: {s},
                        \\
                    , .{ field.name, type_str });
                },
                else => {
                    std.log.err(
                        "Cannot determine default value for {s}.{s} with type: {any}",
                        .{ struct_name, field.name, t },
                    );
                },
            }
        },
        .pointer => |_| {
            w.write(
                \\    {s}: ?{s} = null,
                \\
            , .{ field.name, type_str });
        },
        .array => |array| {
            const array_base_type = type_db.get_type(array.base_type_idx);
            if (array_base_type.is_builtin()) {
                w.write(
                    \\    {[name]s}: {[type]s} = @import("std").mem.zeroes({[type]s}),
                    \\
                , .{ .name = field.name, .type = type_str });
            } else {
                const base_type_str = try type_db.type_string(alloc, array.base_type_idx);
                var s: []const u8 = &.{};
                const enum_idx = array_base_type.enum_idx();
                if (enum_idx != .none) {
                    const e = type_db.get_enum(enum_idx);
                    s = try std.fmt.allocPrint(alloc, ".{{{[base_type]s}.{[default]s}}}", .{
                        .base_type = base_type_str,
                        .default = e.values[0].name,
                    });
                }
                const struct_idx = array_base_type.struct_idx();
                if (struct_idx != .none) {
                    s = try std.fmt.allocPrint(alloc, ".{{{[base_type]s}{{}}}}", .{
                        .base_type = base_type_str,
                    });
                }
                const handle_idx = array_base_type.handle_idx();
                if (handle_idx != .none) {
                    s = try std.fmt.allocPrint(alloc, ".{{{[base_type]s}.none}}", .{
                        .base_type = base_type_str,
                    });
                }
                switch (array.len) {
                    .string => |string| {
                        w.write(
                            \\    {[name]s}: {[type]s} = {[s]s} ** {[len]s},
                            \\
                        , .{
                            .name = field.name,
                            .type = type_str,
                            .s = s,
                            .len = string[1 .. string.len - 1],
                        });
                    },
                    .type_idx => |tidx| {
                        const ts = try type_db.type_string(alloc, tidx);
                        w.write(
                            \\    {[name]s}: {[type]s} = {[s]s} ** {[len]s},
                            \\
                        , .{
                            .name = field.name,
                            .type = type_str,
                            .s = s,
                            .len = ts,
                        });
                    },
                }
            }
        },
        .alias => |alias| {
            try write_single_struct_field(alloc, type_db, w, struct_name, field, alias.type_idx);
        },
        .placeholder => |placeholder| {
            std.log.err(
                "Cannot determine default value for {s}.{s} with type: {any} found placeholder for {s}",
                .{ struct_name, field.name, t, placeholder },
            );
        },
        else => unreachable,
    }
}

fn write_unions(alloc: Allocator, w: *Writer, type_db: *TypeDatabase) !void {
    w.write(
        \\
        \\// Unions
        \\
    , .{});

    for (type_db.unions.items) |@"union"| {
        for (@"union".enabled_by_extensions) |ext| w.write(
            \\// Extension: {s}
            \\
        , .{ext});

        if (@"union".comment) |c| w.write(
            \\// Comment: {s}
            \\
        , .{c});

        w.write(
            \\pub const {s} = extern union {{
            \\
        , .{@"union".name});

        for (@"union".members) |member| {
            if (member.selection) |s| w.write(
                \\    // Selected with: {s}
                \\
            , .{s});
            if (member.len_expression) |l| w.write(
                \\    // Length expression: {s}
                \\
            , .{l});

            const type_str = try type_db.type_string(alloc, member.type_idx);
            w.write(
                \\    {s}: {s},
                \\
            , .{ member.name, type_str });
        }

        w.write(
            \\}};
            \\
        , .{});
    }
}

fn write_functions(alloc: Allocator, w: *Writer, type_db: *TypeDatabase) !void {
    w.write(
        \\
        \\// Commands
        \\
    , .{});

    for (type_db.functions.items) |*function| {
        if (function.queues) |c| w.write(
            \\// Queues: {s}
            \\
        , .{c});
        if (function.successcodes) |c| w.write(
            \\// Success codes: {s}
            \\
        , .{c});
        if (function.errorcodes) |c| w.write(
            \\// Error codes: {s}
            \\
        , .{c});
        if (function.renderpass) |c| w.write(
            \\// Render pass: {s}
            \\
        , .{c});
        if (function.videocoding) |c| w.write(
            \\// Video conding: {s}
            \\
        , .{c});
        if (function.cmdbufferlevel) |c| w.write(
            \\// Command buffer levels: {s}
            \\
        , .{c});
        if (function.conditionalrendering) |c| w.write(
            \\// Conditional rendering: {}
            \\
        , .{c});
        w.write(
            \\// Can be used without queues: {}
            \\
        , .{function.allownoqueues});
        if (function.comment) |c| w.write(
            \\// Comment: {s}
            \\
        , .{c});

        w.write(
            \\pub const {s} = fn (
            \\
        , .{function.name});
        for (function.parameters) |parameter| {
            if (parameter.len_expression) |l| w.write(
                \\    // Length expression: {s}
                \\
            , .{l});
            if (parameter.valid_structs) |vs| w.write(
                \\    // valid structs: {s}
                \\
            , .{vs});

            const t = type_db.get_type_follow_alias(parameter.type_idx);
            const is_pointer = t.* == .pointer;
            const type_str = try type_db.type_string(alloc, parameter.type_idx);
            var optional_str: []const u8 = &.{};
            if (parameter.optional and is_pointer)
                optional_str = "?";
            w.write(
                \\    {s}: {s}{s},
                \\
            , .{ parameter.name, optional_str, type_str });
        }

        const type_str = try type_db.type_string(alloc, function.return_type_idx);
        var optional_str: []const u8 = &.{};
        const t = type_db.get_type_follow_alias(function.return_type_idx);
        if (t.* == .pointer)
            optional_str = "?";
        w.write(
            \\) callconv(.c) {s}{s};
            \\
        , .{ optional_str, type_str });
    }
}

fn write_aliases(alloc: Allocator, w: *Writer, type_db: *const TypeDatabase) !void {
    w.write(
        \\
        \\// Aliases
        \\
    , .{});
    for (type_db.types.items) |t| {
        if (t == .alias) {
            const alias = t.alias;
            const name = try type_db.type_string(alloc, alias.type_idx);
            w.write(
                \\pub const {s} = {s};
                \\
            , .{ alias.name, name });
        }
    }
}

fn write_unknown_types(w: *Writer, type_db: *const TypeDatabase) void {
    w.write(
        \\
        \\// Unknown types
        \\
    , .{});
    for (type_db.types.items) |t| {
        switch (t) {
            .placeholder => |name| {
                w.write(
                    \\pub const {[name]s} = if (@hasDecl(@import("root"), "{[name]s}")) @import("root").{[name]s} else blk: {{
                    \\    break :blk struct {{}};
                    \\}};
                    \\
                , .{ .name = name });
            },
            else => {},
        }
    }
}

fn write_extensions(w: *Writer, db: *const XmlDatabase) void {
    w.write(
        \\
        \\// Extensions
        \\
    , .{});
    for (db.extensions.items) |ext| {
        w.write(
            \\pub const {[name]s}_name = "{[name]s}";
            \\// Extension: {[name]s}
            \\// Number: {[number]s}
            \\// Type: {[type]t}
            \\
        , .{
            .name = ext.name,
            .number = ext.number,
            .type = ext.type,
        });
        if (ext.author) |v| w.write(
            \\// Author: {s}
            \\
        , .{v});
        if (ext.depends) |v| w.write(
            \\// Depends: {s}
            \\
        , .{v});
        if (ext.platform) |v| w.write(
            \\// Platform: {s}
            \\
        , .{v});
        w.write(
            \\// Supported: {t}
            \\
        , .{ext.supported});
        if (ext.promotedto) |v| w.write(
            \\// Promoted to: {s}
            \\
        , .{v});
        if (ext.deprecatedby) |v| w.write(
            \\// Deprecated by: {s}
            \\
        , .{v});
        if (ext.obsoletedby) |v| w.write(
            \\// Obsoleted by: {s}
            \\
        , .{v});
        if (ext.comment) |v| {
            w.write(
                \\// Comment:
                \\
            , .{});
            w.write_comment(v, "//     ");
        }
        w.write(
            \\// Unlocks:
            \\
        , .{});

        for (ext.require) |require| {
            if (require.depends) |v| w.write(
                \\//     Depends: {s}
                \\
            , .{v});
            for (require.items) |i| {
                switch (i) {
                    .comment => |v| {
                        w.write(
                            \\//         Comment:
                            \\
                        , .{});
                        w.write_comment(v, "//             ");
                    },
                    .command => |v| {
                        w.write(
                            \\//         Command:
                            \\//             Name: {s}
                            \\
                        , .{v.name});
                        if (v.comment) |vv| {
                            w.write(
                                \\//             Comment:
                                \\
                            , .{});
                            w.write_comment(vv, "//                 ");
                        }
                    },
                    .@"enum" => |v| {
                        w.write(
                            \\//         Enum:
                            \\//             Name: {s}
                            \\//             Negative: {}
                            \\
                        , .{
                            v.name,
                            v.negative[0],
                        });
                        if (v.value) |vv| w.write(
                            \\//             Value: {s}
                            \\
                        , .{vv});
                        if (v.bitpos) |vv| w.write(
                            \\//             Bitpos: {d}
                            \\
                        , .{vv});
                        if (v.extends) |vv| w.write(
                            \\//             Extends: {s}
                            \\
                        , .{vv});
                        if (v.extnumber) |vv| w.write(
                            \\//             Extnumber: {d}
                            \\
                        , .{vv});
                        if (v.offset) |vv| w.write(
                            \\//             Offset: {d}
                            \\
                        , .{vv});
                        if (v.alias) |vv| w.write(
                            \\//             Alias: {s}
                            \\
                        , .{vv});
                        if (v.comment) |vv| {
                            w.write(
                                \\//             Comment:
                                \\
                            , .{});
                            w.write_comment(vv, "//                 ");
                        }
                    },
                    .type => |v| w.write(
                        \\//         Type:
                        \\//             Name: {s}
                        \\
                    , .{v.name}),
                    .feature => |v| {
                        w.write(
                            \\//         Feature:
                            \\//             Name: {s}
                            \\
                        , .{v.name});
                        if (v.@"struct") |vv| w.write(
                            \\//             Struct: {s}
                            \\
                        , .{vv});
                        if (v.comment) |vv| {
                            w.write(
                                \\//             Comment:
                                \\
                            , .{});
                            w.write_comment(vv, "//                 ");
                        }
                    },
                }
            }
        }
    }
}

pub fn enum_offset(extension_number: i32, offset: i32) i32 {
    const BASE = 1000000000;
    const RANGE = 1000;
    const result = BASE + (extension_number - 1) * RANGE + offset;
    return result;
}
