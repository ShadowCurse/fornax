// Copyright (c) 2026 Egor Lazarchuk
// SPDX-License-Identifier: MIT

const std = @import("std");
const log = @import("log.zig");
const control_block = @import("control_block.zig");

const vk = @import("vk.zig");
const vv = @import("vk_validation.zig");

const Allocator = std.mem.Allocator;
const Validation = vv.Validation;
const Database = @import("database.zig");
const Barrier = @import("barrier.zig");

const RootEntry = struct {
    entry: *Database.Entry,
    arena: std.heap.ArenaAllocator,
};
pub fn init_root_entries(alloc: Allocator, db: *Database) ![]RootEntry {
    const graphics_pipelines = db.entries.getPtrConst(.graphics_pipeline).values().len;
    const compute_pipelines = db.entries.getPtrConst(.compute_pipeline).values().len;
    const raytracing_pipelines = db.entries.getPtrConst(.raytracing_pipeline).values().len;

    const total_pipelines = graphics_pipelines + compute_pipelines + raytracing_pipelines;
    const root_entries: []RootEntry = try alloc.alloc(RootEntry, total_pipelines);
    var re = root_entries;
    for (db.entries.getPtr(.graphics_pipeline).values(), re[0..graphics_pipelines]) |*e, *r|
        r.* = .{ .entry = e, .arena = .init(std.heap.page_allocator) };
    re = re[graphics_pipelines..];
    for (db.entries.getPtr(.compute_pipeline).values(), re[0..compute_pipelines]) |*e, *r|
        r.* = .{ .entry = e, .arena = .init(std.heap.page_allocator) };
    re = re[compute_pipelines..];
    for (db.entries.getPtr(.raytracing_pipeline).values(), re[0..raytracing_pipelines]) |*e, *r|
        r.* = .{ .entry = e, .arena = .init(std.heap.page_allocator) };
    return root_entries;
}

pub fn actual_thread_count(num_threads: ?u32) u32 {
    var thread_count: u32 = @truncate(std.Thread.getCpuCount() catch 1);
    if (num_threads) |nt| {
        if (nt != 0) thread_count = nt;
    }
    return thread_count;
}

pub const WorkQueue = struct {
    entries: []RootEntry,
    next_parse: std.atomic.Value(u32) = .init(0),
    next_create: std.atomic.Value(u32) = .init(0),

    const Self = @This();
    pub fn take_next_parse(self: *Self) ?*RootEntry {
        var result: ?*RootEntry = null;
        const next = self.next_parse.fetchAdd(1, .acq_rel);
        if (next < self.entries.len) result = &self.entries[next];
        return result;
    }
    pub fn take_next_create(self: *Self) ?*RootEntry {
        var result: ?*RootEntry = null;
        const next = self.next_create.fetchAdd(1, .acq_rel);
        if (next < self.entries.len) result = &self.entries[next];
        return result;
    }
};

pub const Task = struct {
    root_entry: *RootEntry = undefined,
    queue: std.ArrayListUnmanaged(struct { *Database.Entry, u32 }) = .empty,
    arena: std.heap.ArenaAllocator = undefined,
};
pub const Tasks = struct {
    tasks: [MAX_TASKS]Task = .{Task{}} ** MAX_TASKS,
    current: u8 = 0,

    const MAX_TASKS = 8;
    const Self = @This();

    pub fn next(self: *Self) *Task {
        const task = &self.tasks[self.current];
        self.current += 1;
        self.current %= MAX_TASKS;
        return task;
    }
};

pub const Context = struct {
    arena: std.heap.ArenaAllocator,
    shared_alloc: Allocator,
    progress: *std.Progress.Node,
    barrier: *Barrier,
    db: *Database,
    work_queue: *WorkQueue,
    thread_count: u32,
    validation: *const Validation,
    vk_device: vk.VkDevice,
};

pub fn init_contexts(
    alloc: Allocator,
    shared_alloc: Allocator,
    progress: *std.Progress.Node,
    barrier: *Barrier,
    db: *Database,
    work_queue: *WorkQueue,
    thread_count: u32,
    validation: *const Validation,
    vk_device: vk.VkDevice,
) ![]align(64) Context {
    const contexts = try alloc.alignedAlloc(Context, .@"64", thread_count);
    for (contexts) |*c| {
        c.* = .{
            .arena = .init(std.heap.page_allocator),
            .shared_alloc = shared_alloc,
            .progress = progress,
            .barrier = barrier,
            .db = db,
            .work_queue = work_queue,
            .thread_count = thread_count,
            .validation = validation,
            .vk_device = vk_device,
        };
    }
    return contexts;
}

pub fn spawn_threads(
    alloc: Allocator,
    comptime function: fn (*Context) void,
    contexts: []Context,
) ![]std.Thread {
    const threads = try alloc.alloc(std.Thread, contexts.len);
    for (threads, contexts) |*t, *c| t.* = try std.Thread.spawn(.{}, function, .{c});
    return threads;
}

pub fn parse(comptime P: type, comptime V: type, context: *Context) !void {
    var progress = context.progress.start("parsing", 0);
    defer progress.end();

    const work_queue = context.work_queue;
    const shared_alloc = context.shared_alloc;
    const thread_alloc = context.arena.allocator();
    defer _ = context.arena.reset(.retain_capacity);

    var gpa_allocator: std.heap.DebugAllocator(.{}) = .{ .backing_allocator = thread_alloc };
    const gpa_alloc = gpa_allocator.allocator();

    var tasks: Tasks = .{};
    for (&tasks.tasks) |*t| t.arena = .init(gpa_alloc);
    while (true) {
        defer progress.completeOne();

        const task = tasks.next();
        if (task.queue.items.len == 0) {
            if (work_queue.take_next_parse()) |root_entry| {
                log.debug(
                    @src(),
                    "Adding new parse task: {t} 0x{x:0>16}",
                    .{ root_entry.entry.tag, root_entry.entry.hash },
                );
                task.root_entry = root_entry;
                task.queue = .empty;
                _ = task.arena.reset(.retain_capacity);
                try task.queue.append(task.arena.allocator(), .{ root_entry.entry, 0 });
            }
        }
        for (&tasks.tasks) |*t| {
            if (t.queue.items.len != 0) break;
        } else {
            break;
        }

        const tmp_alloc = task.arena.allocator();
        while (task.queue.pop()) |tuple| {
            const curr_entry, const next_dep = tuple;

            switch (curr_entry.parse(
                P,
                V,
                shared_alloc,
                task.root_entry.arena.allocator(),
                thread_alloc,
                context.db,
                context.validation,
            )) {
                .parsed => {
                    if (next_dep != curr_entry.dependencies.len) {
                        try task.queue.append(tmp_alloc, .{ curr_entry, next_dep + 1 });
                        const dep = curr_entry.dependencies[next_dep];
                        try task.queue.append(tmp_alloc, .{ dep.entry, 0 });
                    }
                },
                .parsing => {
                    try task.queue.append(tmp_alloc, .{ curr_entry, next_dep });
                    break;
                },
                .invalid => {
                    log.debug(
                        @src(),
                        "Encountered invalid entry during parsing {t} 0x{x:0>16}",
                        .{ curr_entry.tag, curr_entry.hash },
                    );
                    curr_entry.decrement_dependencies();
                    while (task.queue.pop()) |t| {
                        const e, _ = t;
                        log.debug(
                            @src(),
                            "Invalidating parent: {t} 0x{x:0>16}",
                            .{ e.tag, e.hash },
                        );
                        e.status.store(.invalid, .release);
                        e.decrement_dependencies();
                    }
                    control_block.record_failed_entry(task.root_entry.entry.tag);
                    break;
                },
            }
        } else {
            control_block.record_parsed_entry(task.root_entry.entry.tag);
        }
    }
}

pub fn create(
    comptime P: type,
    comptime C: type,
    comptime D: type,
    context: *Context,
) !void {
    var progress = context.progress.start("creation", 0);
    defer progress.end();

    const work_queue = context.work_queue;
    const thread_alloc = context.arena.allocator();

    var gpa_allocator: std.heap.DebugAllocator(.{}) = .{ .backing_allocator = thread_alloc };
    const gpa_alloc = gpa_allocator.allocator();

    var tasks: Tasks = .{};
    for (&tasks.tasks) |*t| t.arena = .init(gpa_alloc);
    while (true) {
        defer progress.completeOne();

        const task = tasks.next();
        if (task.queue.items.len == 0) {
            if (work_queue.take_next_create()) |root_entry| {
                log.debug(
                    @src(),
                    "Adding new create task: {t} 0x{x:0>16}",
                    .{ root_entry.entry.tag, root_entry.entry.hash },
                );
                task.root_entry = root_entry;
                task.queue = .empty;
                _ = task.arena.reset(.retain_capacity);
                try task.queue.append(task.arena.allocator(), .{ root_entry.entry, 0 });
            }
        }
        for (&tasks.tasks) |*t| {
            if (t.queue.items.len != 0) break;
        } else {
            break;
        }

        const tmp_alloc = task.arena.allocator();
        while (task.queue.pop()) |tuple| {
            const curr_entry, const next_dep = tuple;

            switch (curr_entry.create(
                P,
                C,
                tmp_alloc,
                context.db,
                context.validation,
                context.vk_device,
            )) {
                .dependencies => {
                    if (next_dep != curr_entry.dependencies.len) {
                        try task.queue.append(tmp_alloc, .{ curr_entry, next_dep + 1 });
                        const dep = curr_entry.dependencies[next_dep];
                        try task.queue.append(tmp_alloc, .{ dep.entry, 0 });
                    }
                },
                .creating => {
                    try task.queue.append(tmp_alloc, .{ curr_entry, next_dep });
                    break;
                },
                .created => {
                    curr_entry.destroy(D, context.vk_device);
                },
                .invalid => {
                    log.debug(
                        @src(),
                        "Encountered invalid entry during creating {t} 0x{x:0>16}",
                        .{ curr_entry.tag, curr_entry.hash },
                    );
                    curr_entry.destroy_dependencies(D, context.vk_device);
                    while (task.queue.pop()) |t| {
                        const e, _ = t;
                        log.debug(
                            @src(),
                            "Invalidating parent: {t} 0x{x:0>16}",
                            .{ e.tag, e.hash },
                        );
                        e.status.store(.invalid, .release);
                        e.destroy_dependencies(D, context.vk_device);
                    }
                    _ = task.arena.reset(.retain_capacity);
                    break;
                },
            }
        } else {
            _ = task.arena.reset(.retain_capacity);
            control_block.record_successful_entry(task.root_entry.entry.tag);
        }
    }
}

test "parse/create" {
    const parsing = @import("parsing.zig");

    const Dummy = struct {
        fn parse(
            _: Allocator,
            _: Allocator,
            _: *const Database,
            _: []const u8,
        ) parsing.Error!parsing.Result {
            unreachable;
        }
        fn parse_with_dependencies(
            _: Allocator,
            _: Allocator,
            _: *const Database,
            _: []const u8,
        ) parsing.Error!parsing.ResultWithDependencies {
            unreachable;
        }

        fn put_pipelines(
            alloc: Allocator,
            db: *Database,
            data: []const struct {
                hash: u32,
                dependent_by: u32 = 0,
                create_info: ?*align(8) const anyopaque = null,
            },
        ) !void {
            db.entries.getPtr(.graphics_pipeline).deinit(alloc);
            db.entries = .initFill(.empty);
            for (data) |d| {
                try db.entries.getPtr(.graphics_pipeline).put(alloc, d.hash, .{
                    .tag = .graphics_pipeline,
                    .hash = d.hash,
                    .payload_flag = .not_compressed,
                    .payload_crc = 0,
                    .payload_stored_size = 0,
                    .payload_decompressed_size = 0,
                    .payload_file_offset = 0,
                    .status = if (d.create_info != null) .init(.parsed) else .init(.not_parsed),
                    .create_info = d.create_info,
                    .dependent_by = .init(d.dependent_by),
                });
            }
        }

        fn create(_: vk.VkDevice, _: *align(8) const anyopaque) !?*anyopaque {
            unreachable;
        }
        fn destroy(_: vk.VkDevice, _: *const anyopaque) void {
            unreachable;
        }

        fn validate(
            _: *const vv.Extensions,
            _: *const vk.VkRayTracingPipelineCreateInfoKHR,
            _: bool,
        ) bool {
            return true;
        }
    };

    const TestValidate = struct {
        pub const validate_VkSamplerCreateInfo = Dummy.validate;
        pub const validate_VkDescriptorSetLayoutCreateInfo = Dummy.validate;
        pub const validate_VkPipelineLayoutCreateInfo = Dummy.validate;
        pub const validate_VkRenderPassCreateInfo = Dummy.validate;
        pub const validate_VkGraphicsPipelineCreateInfo = Dummy.validate;
        pub const validate_VkComputePipelineCreateInfo = Dummy.validate;
        pub const validate_VkRayTracingPipelineCreateInfoKHR = Dummy.validate;
    };

    var arena = std.heap.ArenaAllocator.init(std.heap.page_allocator);
    const alloc = arena.allocator();

    var progress = std.Progress.start(.{});
    defer progress.end();

    var tmp_dir = std.testing.tmpDir(.{});
    defer tmp_dir.cleanup();
    const tmp_file = try tmp_dir.dir.createFile("parse_test", .{});

    // Simple root node with 2 deps
    {
        const TestParse = struct {
            const Global = struct {
                var n: u32 = 0;
                var gp: vk.VkGraphicsPipelineCreateInfo = .{};
            };

            pub const parse_sampler = Dummy.parse;
            pub const parse_descriptor_set_layout = Dummy.parse_with_dependencies;
            pub const parse_pipeline_layout = Dummy.parse_with_dependencies;
            pub const parse_render_pass = Dummy.parse;
            pub const parse_compute_pipeline = Dummy.parse_with_dependencies;
            pub const parse_raytracing_pipeline = Dummy.parse_with_dependencies;
            pub fn parse_graphics_pipeline(
                _: Allocator,
                _: Allocator,
                _: *const Database,
                _: []const u8,
            ) parsing.Error!parsing.ResultWithDependencies {
                defer Global.n += 1;

                var dependencies: []const parsing.Dependency = &.{
                    .{ .tag = .graphics_pipeline, .hash = 1 },
                    .{ .tag = .graphics_pipeline, .hash = 2 },
                };
                if (Global.n != 0) dependencies = &.{};
                return .{
                    .version = 6,
                    .hash = Global.n,
                    .create_info = @ptrCast(&Global.gp),
                    .dependencies = dependencies,
                };
            }
        };

        var db: Database = .{ .file = tmp_file, .entries = .initFill(.empty), .arena = arena };
        try Dummy.put_pipelines(alloc, &db, &.{
            .{ .hash = 1 },
            .{ .hash = 2 },
        });
        var test_entry: Database.Entry = .{
            .tag = .graphics_pipeline,
            .hash = 0,
            .payload_flag = .not_compressed,
            .payload_crc = 0,
            .payload_stored_size = 0,
            .payload_decompressed_size = 0,
            .payload_file_offset = 0,
        };
        var root_entries: [1]RootEntry = .{.{ .entry = &test_entry, .arena = .init(alloc) }};
        var work_queue: WorkQueue = .{ .entries = &root_entries };
        var validation: vv.Validation = undefined;
        var thread_context: Context = .{
            .arena = .init(alloc),
            .shared_alloc = alloc,
            .progress = &progress,
            .barrier = undefined,
            .db = &db,
            .work_queue = &work_queue,
            .thread_count = 1,
            .validation = &validation,
            .vk_device = undefined,
        };
        try parse(TestParse, TestValidate, &thread_context);
        try std.testing.expectEqual(.parsed, test_entry.status.raw);
        const pipelines = db.entries.getPtr(.graphics_pipeline);
        for (pipelines.values()) |*entry| {
            try std.testing.expectEqual(.parsed, entry.status.raw);
            try std.testing.expectEqual(1, entry.dependent_by.raw);
        }
    }

    // Simple root node with 2 deps, one of deps is invalid
    {
        const TestParse = struct {
            const Global = struct {
                var n: u32 = 0;
                var gp: vk.VkGraphicsPipelineCreateInfo = .{};
            };

            pub const parse_sampler = Dummy.parse;
            pub const parse_descriptor_set_layout = Dummy.parse_with_dependencies;
            pub const parse_pipeline_layout = Dummy.parse_with_dependencies;
            pub const parse_render_pass = Dummy.parse;
            pub const parse_compute_pipeline = Dummy.parse_with_dependencies;
            pub const parse_raytracing_pipeline = Dummy.parse_with_dependencies;
            pub fn parse_graphics_pipeline(
                _: Allocator,
                _: Allocator,
                _: *const Database,
                _: []const u8,
            ) parsing.Error!parsing.ResultWithDependencies {
                defer Global.n += 1;

                var dependencies: []const parsing.Dependency = undefined;
                var hash: u32 = undefined;
                switch (Global.n) {
                    0 => {
                        hash = 0;
                        dependencies = &.{
                            .{ .tag = .graphics_pipeline, .hash = 1 },
                            .{ .tag = .graphics_pipeline, .hash = 2 },
                        };
                    },
                    1 => {
                        hash = 1;
                        dependencies = &.{};
                    },
                    2 => {
                        return error.InvalidJson;
                    },
                    else => unreachable,
                }
                return .{
                    .version = 6,
                    .hash = hash,
                    .create_info = @ptrCast(&Global.gp),
                    .dependencies = dependencies,
                };
            }
        };

        var db: Database = .{ .file = tmp_file, .entries = .initFill(.empty), .arena = arena };
        try Dummy.put_pipelines(alloc, &db, &.{
            .{ .hash = 1 },
            .{ .hash = 2 },
        });

        var test_entry: Database.Entry = .{
            .tag = .graphics_pipeline,
            .hash = 0,
            .payload_flag = .not_compressed,
            .payload_crc = 0,
            .payload_stored_size = 0,
            .payload_decompressed_size = 0,
            .payload_file_offset = 0,
        };
        var root_entries: [1]RootEntry = .{.{ .entry = &test_entry, .arena = .init(alloc) }};
        var work_queue: WorkQueue = .{ .entries = &root_entries };
        var validation: vv.Validation = undefined;
        var thread_context: Context = .{
            .arena = .init(alloc),
            .shared_alloc = alloc,
            .progress = &progress,
            .barrier = undefined,
            .db = &db,
            .work_queue = &work_queue,
            .thread_count = 1,
            .validation = &validation,
            .vk_device = undefined,
        };
        try parse(TestParse, TestValidate, &thread_context);
        try std.testing.expectEqual(.invalid, test_entry.status.raw);
        const pipelines = db.entries.getPtr(.graphics_pipeline);
        for (pipelines.values()) |*entry| {
            if (entry.hash == 1) {
                try std.testing.expectEqual(.parsed, entry.status.raw);
                try std.testing.expectEqual(0, entry.dependent_by.raw);
            }
            if (entry.hash == 2) {
                try std.testing.expectEqual(.invalid, entry.status.raw);
                try std.testing.expectEqual(0, entry.dependent_by.raw);
            }
        }
    }

    // Create simple root node with 2 deps
    // Make sure the creation and destruction order is correct
    {
        const Global = struct {
            var create_counter: u32 = 0;
            var destroy_counter: u32 = 0;
            var gp0: u64 = 0xA;
            var gp0_1: u64 = 0;
            var gp0_2: u64 = 0;
            var gp1: u64 = 0xB;
            var gp2: u64 = 0xC;
        };
        const Parse = struct {
            pub const parse_sampler = Dummy.parse;
            pub const parse_shader_module = Dummy.parse;
            pub const parse_descriptor_set_layout = Dummy.parse_with_dependencies;
            pub const parse_pipeline_layout = Dummy.parse_with_dependencies;
            pub const parse_render_pass = Dummy.parse;
            pub const parse_compute_pipeline = Dummy.parse_with_dependencies;
            pub const parse_raytracing_pipeline = Dummy.parse_with_dependencies;
            pub const parse_graphics_pipeline = Dummy.parse_with_dependencies;
        };
        const Create = struct {
            pub const create_vk_sampler = Dummy.create;
            pub const create_descriptor_set_layout = Dummy.create;
            pub const create_pipeline_layout = Dummy.create;
            pub const parse_shader_module = Dummy.create;
            pub const create_shader_module = Dummy.create;
            pub const create_render_pass = Dummy.create;
            pub const create_raytracing_pipeline = Dummy.create;
            pub const create_compute_pipeline = Dummy.create;
            pub fn create_graphics_pipeline(
                _: vk.VkDevice,
                create_info: *align(8) const anyopaque,
            ) !?*anyopaque {
                defer Global.create_counter += 1;
                const c: *const u64 = @ptrCast(create_info);
                switch (Global.create_counter) {
                    0 => try std.testing.expectEqual(0xB, c.*),
                    1 => try std.testing.expectEqual(0xC, c.*),
                    2 => try std.testing.expectEqual(0xA, c.*),
                    else => unreachable,
                }
                return @ptrFromInt(c.*);
            }
        };
        const Destroy = struct {
            pub const destroy_vk_sampler = Dummy.destroy;
            pub const destroy_descriptor_set_layout = Dummy.destroy;
            pub const destroy_pipeline_layout = Dummy.destroy;
            pub const parse_shader_module = Dummy.destroy;
            pub const destroy_shader_module = Dummy.destroy;
            pub const destroy_render_pass = Dummy.destroy;
            pub fn destroy_pipeline(_: vk.VkDevice, handle: *const anyopaque) void {
                defer Global.destroy_counter += 1;
                const c: u64 = @intFromPtr(handle);
                switch (Global.destroy_counter) {
                    0 => std.testing.expectEqual(0xA, c) catch unreachable,
                    1 => std.testing.expectEqual(0xB, c) catch unreachable,
                    2 => std.testing.expectEqual(0xC, c) catch unreachable,
                    else => unreachable,
                }
            }
        };

        var db: Database = .{ .file = tmp_file, .entries = .initFill(.empty), .arena = arena };
        try Dummy.put_pipelines(
            alloc,
            &db,
            &.{
                .{ .hash = 0xB, .dependent_by = 1, .create_info = &Global.gp1 },
                .{ .hash = 0xC, .dependent_by = 1, .create_info = &Global.gp2 },
            },
        );
        var test_entry: Database.Entry = .{
            .tag = .graphics_pipeline,
            .hash = 0xA,
            .payload_flag = .not_compressed,
            .payload_crc = 0,
            .payload_stored_size = 0,
            .payload_decompressed_size = 0,
            .payload_file_offset = 0,
            .status = .init(.parsed),
            .create_info = &Global.gp0,
            .dependencies = &.{
                .{
                    .entry = db.entries.getPtr(.graphics_pipeline).getPtr(0xB).?,
                    .ptr_to_handle = @ptrCast(&Global.gp0_1),
                },
                .{
                    .entry = db.entries.getPtr(.graphics_pipeline).getPtr(0xC).?,
                    .ptr_to_handle = @ptrCast(&Global.gp0_2),
                },
            },
        };
        var root_entries: [1]RootEntry = .{.{ .entry = &test_entry, .arena = .init(alloc) }};
        var work_queue: WorkQueue = .{ .entries = &root_entries };
        var thread_context: Context = .{
            .arena = .init(alloc),
            .shared_alloc = alloc,
            .progress = &progress,
            .barrier = undefined,
            .db = &db,
            .work_queue = &work_queue,
            .thread_count = 1,
            .validation = undefined,
            .vk_device = undefined,
        };
        try create(Parse, Create, Destroy, &thread_context);

        try std.testing.expectEqual(0xB, Global.gp0_1);
        try std.testing.expectEqual(0xC, Global.gp0_2);
        try std.testing.expectEqual(.created, test_entry.status.raw);
        try std.testing.expectEqual(true, test_entry.dependencies_destroyed.raw);
        const pipelines = db.entries.getPtr(.graphics_pipeline);
        for (pipelines.values()) |*entry| {
            try std.testing.expectEqual(.created, entry.status.raw);
            try std.testing.expectEqual(0, entry.dependent_by.raw);
            try std.testing.expectEqual(true, entry.dependencies_destroyed.raw);
        }
    }

    // Create simple root node with 2 deps, but one is uncreatable
    // Make sure the creation and destruction order is correct
    {
        const Global = struct {
            var create_counter: u32 = 0;
            var destroy_counter: u32 = 0;
            var gp0: u64 = 0xA;
            var gp0_1: u64 = 0;
            var gp0_2: u64 = 0;
            var gp1: u64 = 0xB;
            var gp2: u64 = 0xC;
        };
        const Parse = struct {
            pub const parse_sampler = Dummy.parse;
            pub const parse_shader_module = Dummy.parse;
            pub const parse_descriptor_set_layout = Dummy.parse_with_dependencies;
            pub const parse_pipeline_layout = Dummy.parse_with_dependencies;
            pub const parse_render_pass = Dummy.parse;
            pub const parse_compute_pipeline = Dummy.parse_with_dependencies;
            pub const parse_raytracing_pipeline = Dummy.parse_with_dependencies;
            pub const parse_graphics_pipeline = Dummy.parse_with_dependencies;
        };
        const Create = struct {
            pub const create_vk_sampler = Dummy.create;
            pub const create_descriptor_set_layout = Dummy.create;
            pub const create_pipeline_layout = Dummy.create;
            pub const parse_shader_module = Dummy.create;
            pub const create_shader_module = Dummy.create;
            pub const create_render_pass = Dummy.create;
            pub const create_raytracing_pipeline = Dummy.create;
            pub const create_compute_pipeline = Dummy.create;
            pub fn create_graphics_pipeline(
                _: vk.VkDevice,
                create_info: *align(8) const anyopaque,
            ) !?*anyopaque {
                defer Global.create_counter += 1;
                const c: *const u64 = @ptrCast(create_info);
                switch (Global.create_counter) {
                    0 => try std.testing.expectEqual(0xB, c.*),
                    1 => {
                        try std.testing.expectEqual(0xC, c.*);
                        return error.SomeError;
                    },
                    2 => try std.testing.expectEqual(0xA, c.*),
                    else => unreachable,
                }
                return @ptrFromInt(c.*);
            }
        };
        const Destroy = struct {
            pub const destroy_vk_sampler = Dummy.destroy;
            pub const destroy_descriptor_set_layout = Dummy.destroy;
            pub const destroy_pipeline_layout = Dummy.destroy;
            pub const parse_shader_module = Dummy.destroy;
            pub const destroy_shader_module = Dummy.destroy;
            pub const destroy_render_pass = Dummy.destroy;
            pub fn destroy_pipeline(_: vk.VkDevice, handle: *const anyopaque) void {
                defer Global.destroy_counter += 1;
                const c: u64 = @intFromPtr(handle);
                switch (Global.destroy_counter) {
                    0 => std.testing.expectEqual(0xB, c) catch unreachable,
                    else => unreachable,
                }
            }
        };

        var db: Database = .{ .file = tmp_file, .entries = .initFill(.empty), .arena = arena };
        try Dummy.put_pipelines(
            alloc,
            &db,
            &.{
                .{ .hash = 0xB, .dependent_by = 1, .create_info = &Global.gp1 },
                .{ .hash = 0xC, .dependent_by = 1, .create_info = &Global.gp2 },
            },
        );
        var test_entry: Database.Entry = .{
            .tag = .graphics_pipeline,
            .hash = 0xA,
            .payload_flag = .not_compressed,
            .payload_crc = 0,
            .payload_stored_size = 0,
            .payload_decompressed_size = 0,
            .payload_file_offset = 0,
            .status = .init(.parsed),
            .create_info = &Global.gp0,
            .dependencies = &.{
                .{
                    .entry = db.entries.getPtr(.graphics_pipeline).getPtr(0xB).?,
                    .ptr_to_handle = @ptrCast(&Global.gp0_1),
                },
                .{
                    .entry = db.entries.getPtr(.graphics_pipeline).getPtr(0xC).?,
                    .ptr_to_handle = @ptrCast(&Global.gp0_2),
                },
            },
        };
        var root_entries: [1]RootEntry = .{.{ .entry = &test_entry, .arena = .init(alloc) }};
        var work_queue: WorkQueue = .{ .entries = &root_entries };
        var thread_context: Context = .{
            .arena = .init(alloc),
            .shared_alloc = alloc,
            .progress = &progress,
            .barrier = undefined,
            .db = &db,
            .work_queue = &work_queue,
            .thread_count = 1,
            .validation = undefined,
            .vk_device = undefined,
        };
        try create(Parse, Create, Destroy, &thread_context);

        try std.testing.expectEqual(0, Global.gp0_1);
        try std.testing.expectEqual(0, Global.gp0_2);
        try std.testing.expectEqual(.invalid, test_entry.status.raw);
        try std.testing.expectEqual(true, test_entry.dependencies_destroyed.raw);
        const pipelines = db.entries.getPtr(.graphics_pipeline);
        for (pipelines.values()) |*entry| {
            switch (entry.hash) {
                0xB => {
                    try std.testing.expectEqual(.created, entry.status.raw);
                    try std.testing.expectEqual(0, entry.dependent_by.raw);
                    try std.testing.expectEqual(true, entry.dependencies_destroyed.raw);
                },
                0xC => {
                    try std.testing.expectEqual(.invalid, entry.status.raw);
                    try std.testing.expectEqual(0, entry.dependent_by.raw);
                    try std.testing.expectEqual(true, entry.dependencies_destroyed.raw);
                },
                else => unreachable,
            }
        }
    }
}
